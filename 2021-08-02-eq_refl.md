---
layout: post
title: eq_refl
date: 2021-08-02
categories: Coq
---

# 1. eq_refl 

Far and away the most confusing topic to me in Coq has been equality. When you, reader,
   first came across equalities, you were probably told that you can solve simple equalities 
   by simplifying them, and then once the goal looks like `?x = ?x`, you can finish with 
   magic `reflexivity`.
 
{% highlight Coq %}
Lemma one_one_two: 1 + 1 = 2.
Proof using.
  simpl.
  reflexivity.

{% endhighlight %}
In fact, we don't need the `simpl` at all. `reflexivity` can solve the goal all by 
   itself. 

{% highlight Coq %}
  Restart.
  reflexivity.

{% endhighlight %}
One might assume that reflexivity is doing some complicated work behind the curtains -
   including simplification. This is not the case, but one could certainly be excused for 
   thinking so. 

{% highlight Coq %}
Defined.

{% endhighlight %}
Of course, not all equalities are so easy. For instance, let's try throwing reflexivity 
   at some fact about lists. 

{% highlight Coq %}
Require Import Coq.Lists.List.
Import ListNotations.

Theorem app_nil_r {A}: forall l: list A,
  l ++ [] = l.
Proof using.
  intros *.
  Fail reflexivity.
(* The command has indeed failed with message:
   In environment
   A : Type
   l : list A
   Unable to unify "l" with "l ++ []".
 *)

{% endhighlight %}
What separates these two proof goals? Why can reflexivity solve the first, but not 
   this one? One might suspect again that it is a matter of simplification. After all,
   `simpl` fails to progress here: 

{% highlight Coq %}
Fail progress simpl.

(* The command has indeed failed with message:
   Failed to progress
 *)
{% endhighlight %}
But this is somewhat of a red herring. `reflexivity` can in a sense do some simplication,
   but it is not doing it so explicitly. But we're getting ahead of ourselves. Let's take 
   a step back and look at the definition of equality. 

{% highlight Coq %}
Abort.

Locate "_ = _".
(* Notation
   "x = y" := eq x y : type_scope (default interpretation)
 *)

Print eq.

(* Inductive eq (A : Type) (x : A) : A -> Prop :=  eq_refl : x = x *)

{% endhighlight %}
If you haven't already seen the definition of eq, this may look surprising. The type takes 
   two arguments, the only constructor, `eq_refl`, takes just one, producing the reflexive 
   equality. How is it that we are able to prove equalities of two terms that are not exactly
   identical structurally?

   For instance, let's try manually constructing a term of type `1 + 1 = 2`. 
{% highlight Coq %}
   
Check eq_refl (1 + 1).

(* eq_refl : 1 + 1 = 1 + 1 *)

{% endhighlight %}
Well that didn't work. I suppose we can try using the right hand side instead. 

{% highlight Coq %}
Check eq_refl 2.

(* eq_refl : 2 = 2 *)

{% endhighlight %}
That didn't work either. What are we missing? To find out, let's cheat and look at the 
   proof term generated by `reflexivity` in our first proof. 

{% highlight Coq %}
Print one_one_two.

(* one_one_two = eq_refl
	 : 1 + 1 = 2
 *)
   
{% endhighlight %}
So reflexivity does generate a simple `eq_refl` term, but somehow it has the right type?
   What magic is going on here? The printed term is in fact hiding `eq_refl`'s argument,
   so I'll provide a more explicit alternative: 

{% highlight Coq %}
Check (eq_refl 2 : 1 + 1 = 2).

{% endhighlight %}
This is the same proof term we tried before, but this time we are explicitly giving it our 
   own type, which Coq accepts. Indeed, the magic of equality is in the formulation of Coq's
   type system (or perhaps more accurately, the Calculus of Inductive Construction's type
   system). The most straightforward type of `eq_refl 2` would be `2 = 2`, and so that is 
   what Coq originally showed us when we let Coq pick the type. However, Coq permits the 
   type `1 + 1 = 2`, because `1 + 1` and `2` are *definitionally* equal, and therefore 
   interchangeable within the type. 

   What does it mean for two terms to be definitionally equal? It means that there exists some 
   sequence of reduction steps such that the two terms reduce to identical terms (up to 
   irrelevant subterms).

   `1 + 1` reduces to `2` straightforwardly: 

{% highlight Coq %}
Goal 1 + 1 = 2.
  cbv delta.
  cbv iota.
  cbv beta.
  cbv iota.
  cbv beta.
  cbv iota.
  cbv beta.
  cbv iota.
  exact (eq_refl _).

{% endhighlight %}
Of course, we don't actually need to perform each reduction step. Coq reduces the terms for 
   us to type check the proof term. We could have just performed this final application 
   of `eq_refl`. We can still leave out the argument, since Coq will infer it from the type. 

{% highlight Coq %}
  Restart.
  exact (eq_refl _).
Qed.

{% endhighlight %}
We can see that `reflexivity` isn't actually doing that much work. It appears to be
   essentially performing `exact (eq_refl _)`. I don't know about you, but this was much 
   simpler than I was expecting!

   Definitional equality is sometimes also called convertibility. If you want to learn more
   about, you can check out the docs page:
     https://coq.inria.fr/refman/language/core/conversion.html#term-convertible
   
   Let's finish by returning to that proposition about appending nil. 

{% highlight Coq %}
Theorem app_nil_r {A}: forall l: list A,
  l ++ [] = l.
Proof using.
  intros *.
  Fail exact (eq_refl _).
 
(* The term "eq_refl" has type "l ++ [] = l ++ []"
   while it is expected to have type "l ++ [] = l" (cannot unify 
   "l ++ []" and "l")
 *)

{% endhighlight %}
Trying to apply `eq_refl` failed for the same reason that `reflexivity` failed here.
   The left and right hand sides are not definitionally equal. In particular, the left 
   hand side cannot reduce far enough, because of the way the append operation is 
   defined. 

{% highlight Coq %}
  unfold "++".
  Fail progress cbv.

{% endhighlight %}
As expected, we can't reduce further. Let's tidy back up. 
{% highlight Coq %}
  
  fold (l ++ []).

{% endhighlight %}
The crucial intuition here is that the left and right hand side *would* be definitionally 
   equal if we knew the value of `l`.

   This motivates our proof by induction on `l`, thereby exposing its structure. 

{% highlight Coq %}
  induction l.
  - exact (eq_refl _).
  - cbn.
    apply f_equal.
    assumption.
Qed.

{% endhighlight %}
Up next, how would one prove *inequality* manually? 

